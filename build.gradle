group = 'org.sagebionetworks'
ext.name = 'audit-pipeline'
version = '0.0.1.' + (new Date()).format(
        'yyyyMMddHHmmss', TimeZone.getTimeZone("UTC"))

/*** Java ***/

apply plugin: 'java'

jar {
    group = project.group
    baseName = project.ext.name
    version = project.version
}

repositories {
    mavenCentral()
    maven {
        url "http://sagebionetworks.artifactoryonline.com/sagebionetworks/libs-releases-local"
    }
}

dependencies {
    compile 'org.sagebionetworks:dashboard-core:0.1.3+'
    compile 'mysql:mysql-connector-java:5.1.31'
    compile 'org.apache.commons:commons-dbcp2:2.0.1'
    compile 'commons-io:commons-io:2.4'
    compile 'org.springframework:spring-jdbc:3.2.10.RELEASE'
    compile 'org.springframework:spring-context:3.2.10.RELEASE'
    compile 'org.slf4j:slf4j-api:1.7.7'
    compile 'org.apache.httpcomponents:httpclient:4.3.5'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.2.+'
    compile 'com.fasterxml.jackson.core:jackson-core:2.2.+'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.2.+'    
    runtime 'ch.qos.logback:logback-classic:1.1.2'
    runtime 'ch.qos.logback:logback-core:1.1.2'
    testCompile 'junit:junit:4.11'
    testCompile 'org.springframework:spring-test:3.2.10.RELEASE'
    testRuntime 'org.slf4j:slf4j-log4j12:1.7.7'
}

/*** Eclipse ***/

apply plugin: 'eclipse'

/*** Command-line Application ***/

apply plugin: 'application'
mainClassName = "org.sagebionetworks.audit.App"
run {
    args = []
}

/*** Code Quality Tools ***/

apply plugin: 'findbugs'
tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

apply plugin: 'jdepend'
tasks.withType(JDepend) {
    reports {
        xml.enabled = false
        text.enabled = true
    }
}

apply plugin: 'pmd'
tasks.withType(Pmd) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

apply plugin: 'jacoco'

/*** Artifactory ***/

buildscript {
    repositories {
         jcenter()
    }
    dependencies {
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:3.0.1'
    }
}

apply plugin: 'com.jfrog.artifactory'
artifactory {
    contextUrl = 'http://sagebionetworks.artifactoryonline.com/sagebionetworks'
    publish {
        contextUrl = 'http://sagebionetworks.artifactoryonline.com/sagebionetworks'
        repository {
            repoKey = 'libs-releases-local'
            username = "${System.env.ARTI_USR}"
            password = "${System.env.ARTI_PWD}"
        }
        defaults {
            publications ('mavenJava')
        }
    }
}

/*** Maven Publishing ***/

apply plugin: 'maven-publish'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

/*** Wrapper ***/

task wrapper(type: Wrapper) {
    gradleVersion = '2.2'
}
